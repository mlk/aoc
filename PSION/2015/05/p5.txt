PROC part2:
	local error%,phandle%,txt$(25),level%, len%, total&

  error% = IOopen(phandle%, "d:\2015\05\input.txt", $0020)	  
  if error% < 0 
  	raise error%
  endif
	while 1 :
		error% = ioread(phandle%, addr(txt$) + 1, 25)
		if error% < 0
			if error% <> -36
				print "read error"
				print error%
			endif
			break
		endif
		pokeb addr(txt$), error%
		REM print txt$
		if nice%:(txt$)
			total& = total& + 1
		endif
		
	endwh
	print total&
	GET
ENDP

proc nice%:(name$)
	return hasDoubleDoubes%:(name$) AND hasDoubleLetters%:(name$) 
endp

proc hasDoubleDoubes%:(name$)
	local idx%, idx2%, len%, findThis$(2), cur$(2)
	len% = len(name$)
	idx% = 1
	while idx% <= len% - 3
		findThis$ = mid$(name$, idx%, 2)
		idx2% = idx% + 2
		while idx2% <= len% - 1
			cur$ = mid$(name$, idx2%, 2)
						
			if cur$ = findThis$
				return 1
			endif
			idx2% = idx2% + 1
		endwh	
		idx% = idx% + 1
	endwh
	
	return 0
endp

proc hasDoubleLetters%:(name$)
	local idx%, len%, prev$(1), cur$(1)
	len% = len(name$)
	idx% = 3

	while idx% <= len%
		prev$ = mid$(name$, idx% - 2, 1)

		cur$ = mid$(name$, idx%, 1)

		if cur$ = prev$
			return 1
		endif
		idx% = idx% + 1
	endwh
	
	return 0
endp

